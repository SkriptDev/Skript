function ExprXOf(c: number) :: item:
	return {_c} of diamond

test "X of":
	set {_x} to 5
	assert 1 of {_x} is not set with "incorrect type failed"
	set {_z} to itemstack of stone
	assert 2 of {_z} is 2 of itemstack of stone with "item failed"

	assert ExprXOf(2) is 2 of itemstack of diamond with "function return failed - got '%ExprXOf(2)%'"

	set {_i} to 15 of stick
	assert {_i} = itemstack of 15 of stick with "Item should have been 15 sticks"
	assert material of {_i} = stick with "Material of item should have been a stick"
	assert item amount of {_i} = 15 with "Item amount of item should be 15"

	# ItemStack - make sure we cant make invalid itemstacks
	delete {_i}
	set {_i} to itemstack of nether portal
	assert {_i} is not set with "Shouldnt be able to create itemstack of nether portal"
	set {_i} to 1 of nether portal
	assert {_i} is not set with "Shouldnt be able to create itemstack of nether portal"
	set {_i} to 1 of sand
	assert {_i} is set with "We can make an itemstack of sand"
	assert material of {_i} = sand with "The item should be sane"
	delete {_i}
	set {_i} to 0 of stick
	assert {_i} is not set with "ItemStack shouldn't have an amount less than 1"
